plugins {
	id "net.neoforged.gradle.userdev"
	id "info.u_team.gradle_files"
	id "me.modmuss50.mod-publish-plugin"
	id "maven-publish"
}

gradlefiles.loaderSuffix = "neoforge"

group = rootProject.group
base.archivesName = gradlefiles.archivesName()
version = gradlefiles.version()

sourceSets {
	main {
		java {
			srcDirs += "src/integration/java"
			srcDirs += gradlefiles.project("common").sourceSets.common.java.srcDirs
		}
		resources {
			srcDirs += "src/integration/resources"
			srcDirs += gradlefiles.project("common").sourceSets.common.resources.srcDirs
		}
	}
	data {
		java
		compileClasspath += main.compileClasspath + main.output
		runtimeClasspath += main.runtimeClasspath
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
	withSourcesJar()
	withJavadocJar()
}

subsystems {
	parchment {
		minecraftVersion = config.neoforge.parchment.minecraft_version
		mappingsVersion = config.neoforge.parchment.version
	}
}

runs {
	sourceSets.main.runs.modIdentifier = config.mod.modid
	sourceSets.data.runs.modIdentifier = config.mod.modid
	
	client {
		workingDirectory file("run/client").with { mkdirs(); it }
		
		jvmArguments "-Dmixin.debug.verbose=true"
		jvmArguments "-Dmixin.debug.export=true"
		
		modSources sourceSets.main
	}
	
	server {
		workingDirectory file("run/server").with { mkdirs(); it }
		
		jvmArguments "-Dmixin.debug.verbose=true"
		jvmArguments "-Dmixin.debug.export=true"
		
		modSources sourceSets.main
	}
	
	data {
		workingDirectory file("run/data").with { mkdirs(); it }
		
		programArguments "--mod", config.mod.modid
		programArguments "--all"
		programArguments "--output", gradlefiles.project("common").file("src/common_generated/resources").absolutePath
		programArguments "--validate"
		programArguments "--existing", gradlefiles.project("common").file("src/common/resources").absolutePath
		
		systemProperties "coremod.uteamcore.dataprovider-set-indent", "coremod.uteamcore.dataprovider-set-indent.value"
		systemProperties "coremod.uteamcore.dataprovider-set-indent.value", "true"
		
		modSources sourceSets.main
		modSources sourceSets.data
	}
}

tasks.withType(Jar).configureEach { task ->
	gradlefiles.defaultJar(task)
}

tasks.withType(Javadoc).configureEach { task ->
	task.options.addStringOption("Xdoclint:none", "-quiet")
}

signjar.sign(tasks.named("jar"))

processResources {
	doLast {
		fileTree(dir: outputs.files.asPath, include: "**/*.json").each { File file ->
			file.text = groovy.json.JsonOutput.toJson(new groovy.json.JsonSlurper().parse(file))
		}
	}
}

repositories {
	maven {
		url = "https://maven.blamejared.com"
	}
	maven {
		url = "https://maven.octo-studios.com/releases"
	}
}

dependencies {
	implementation gradlefiles.neoForgeDependency()
	
	implementation "info.u-team:u_team_core-neoforge-1.21:5.6.0.317"
	
	compileOnly "mezz.jei:jei-${config.minecraft.version}-neoforge-api:${config.jei.version}"
	compileOnly "mezz.jei:jei-${config.minecraft.version}-common-api:${config.jei.version}"
	runtimeOnly "mezz.jei:jei-${config.minecraft.version}-neoforge:${config.jei.version}"
	
	compileOnly "top.theillusivec4.curios:curios-neoforge:${config.curios_continuation.version}:api"
	runtimeOnly "top.theillusivec4.curios:curios-neoforge:${config.curios_continuation.version}"
}

publishMods {
	file = tasks.named("jar").flatMap { jar -> jar.archiveFile }
	displayName = "${config.publish.display}-NeoForge-${config.minecraft.version}-${project.version}"
	changelog = "[Changelog for version ${project.version}](${gradlefiles.changelogUrl()})"
	type = me.modmuss50.mpp.ReleaseType.of(config.publish.type)
	modLoaders.add("neoforge")
	
	curseforge {
		accessToken = defaultPropertyValue("curseforge.token")
		
		projectId = config.curse.id
		projectSlug = config.curse.slug
		minecraftVersions.add(config.minecraft.version)
		javaVersions.add(JavaVersion.VERSION_21)
		
		requires("u-team-core")
		optional("jei")
		optional("curios-continuation")
	}
	
	modrinth {
		accessToken = defaultPropertyValue("modrinth.token")
		
		projectId = config.modrinth.id
		minecraftVersions.add(config.minecraft.version)
		
		requires("u-team-core")
		optional("jei")
		optional("curios-continuation")
	}
	
	discord {
		webhookUrl = defaultPropertyValue("discord.update_webhook")
		
		username = "Mod Update"
		avatarUrl = "https://raw.githubusercontent.com/MC-U-Team/${config.github.name}/${config.github.branch}/logo.png"
		
		content = changelog.map { log -> "# ${config.minecraft.version}: ${config.publish.display} (NeoForge)\n" + log}
	}
}

publishing {
	repositories {
		maven {
			url "https://repo.u-team.info"
			credentials {
				username = "maven"
				password = defaultPropertyValue("maven.password")
			}
		}
	}
	publications {
		mavenJava(MavenPublication) {
			artifactId project.base.archivesName.get()
			
			from gradlefiles.filteredJavaComponent {
				filterDependency { holder ->
					"top.theillusivec4.curios" == holder.dependency.group
				}
				filterDependency { holder ->
					"mezz.jei" == holder.dependency.group
				}
			}
			
			pom {
				pom.name = project.base.archivesName.get()
				rootProject.commonPom(it)
			}
		}
	}
}